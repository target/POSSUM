plugins {
    id 'java'
    id 'maven-publish'
    id 'idea'
    id 'application'
    id 'org.springframework.boot' version '3.4.13'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'de.undercouch.download' version '5.6.0'
    id 'jacoco'
    id 'org.barfuin.gradle.jacocolog' version '3.1.0'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

application {
    mainClassName = 'com.target.devicemanager.DeviceMain'
}

group 'com.target'
version '1.0'

sourceCompatibility = '17'

repositories {
    mavenCentral()
}

ext['log4j2.version'] = '2.24.3'

dependencies {
    implementation(platform("org.apache.logging.log4j:log4j-bom:2.25.3"))
    implementation ("org.springframework.boot:spring-boot-starter-web") {
        exclude group: "org.apache.logging.log4j"
    }
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-loader"
    implementation "org.springframework:spring-webmvc:6.2.11"
    implementation "org.springframework:spring-web:6.2.11"
    implementation "org.springframework:spring-core:6.2.11"
    implementation "org.springframework.security:spring-security-core:6.4.10"
    implementation "org.junit.platform:junit-platform-launcher:1.12.2"
    implementation "org.junit.platform:junit-platform-commons:1.12.2"
    implementation "org.junit.platform:junit-platform-engine:1.12.2"
    implementation "org.javapos:javapos-config-loader:4.0.2"
    implementation "org.javapos:javapos:1.14.4"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.19.0"
    implementation "com.fasterxml.jackson.core:jackson-core:2.19.0"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.19.0"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.19.0"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.19.0"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.19.0"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:2.19.0"
    implementation "jakarta.servlet:jakarta.servlet-api:6.1.0"
    implementation "jakarta.validation:jakarta.validation-api:3.1.1"
    implementation "jakarta.annotation:jakarta.annotation-api:3.0.0"
    implementation "org.apache.logging.log4j:log4j-api"
    implementation "org.apache.logging.log4j:log4j-core"
    implementation "org.apache.logging.log4j:log4j-1.2-api"
    implementation "org.apache.commons:commons-lang3:3.18.0"

    implementation "net.logstash.logback:logstash-logback-encoder:8.1"
    implementation "org.yaml:snakeyaml:2.4"

    implementation "org.springframework.boot:spring-boot-starter-cache:3.4.9"
    implementation "com.github.ben-manes.caffeine:caffeine:3.2.0"
    implementation "io.github.classgraph:classgraph:4.8.179"
    testImplementation "org.springframework.boot:spring-boot-starter-test:3.4.9"
}

tasks.withType(JavaExec){
    systemProperties System.properties
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging {
        events "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.13"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

build {
    dependsOn("copyRuntimeJars")
}

task copyRuntimeJars(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/libs"
}

shadowJar{
    archiveClassifier.set("")
}

bootJar {
    archiveClassifier.set("")
    manifest {
        attributes(
                'Main-Class': 'org.springframework.boot.loader.launch.PropertiesLauncher'
        )
    }
}

task generatePom {
    doLast {
        pom {
            project {
                licenses {
                    license {
                        name 'MIT'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                        comments 'All source code is under the MIT license.'
                    }
                }
            }
        }.writeTo("$buildDir/pom/$archivesBaseName-$version"+".pom")
    }
}
